event NTLM_Authorizing_Compromised_Account:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and event_src.title == "windows"
        and msgid == "4624"
        and not in_list(["127.0.0.1","::1"], src.ip)
        and logon_type == 3
        and status == "success"
        and lower(logon_service) == "ntlmssp"
        and lower(logon_auth_method) == "remote"
    }

event Download_Part_of_the_Payload:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and filter::ProcessStart_Windows_any()
        and lower(object.process.parent.name) == "w3wp.exe"
        
        # "C:\Windows\System32\cmd.exe" /c echo TVqQAAMAAAAEAAAA...>>%TEMP%\RsJnf.b64
        and lower(object.process.name) == "cmd.exe"
        and regex(lower(object.process.cmdline), "/c echo \S+>>%{1,2}temp%{1,2}\S+\.b64", 0) != null
    }

rule ProxyNotShell: NTLM_Authorizing_Compromised_Account and Download_Part_of_the_Payload+ timer 30s
    init {
        $first_download_event = true
    }

    on NTLM_Authorizing_Compromised_Account {
        $subject.account.name = subject.account.name
        $subject.account.fullname = subject.account.fullname
        $subject.account.domain = subject.account.domain
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges

        $src.asset = src.asset
        $src.host = src.host
        $src.ip = src.ip
        $src.hostname = src.hostname
        $src.fqdn = src.fqdn
        $src.port = src.port
        $src.mac = src.mac

        $dst.asset = dst.asset
        $dst.host = dst.host
        $dst.ip = dst.ip
        $dst.hostname = dst.hostname
        $dst.fqdn = dst.fqdn
        $dst.port = dst.port
        $dst.mac = dst.mac
    }

    on Download_Part_of_the_Payload {
        if $first_download_event then 
            $object.account.session_id = object.account.session_id
            $object.account.name = object.account.name
            $object.account.domain = object.account.domain
            $object.account.id = object.account.id

            $object.process.id = object.process.id
            $object.process.name = object.process.name
            $object.process.path = object.process.path
            $object.process.fullpath = object.process.fullpath
            $object.process.hash = object.process.hash
            $object.process.version = object.process.version
            $object.process.cmdline = object.process.cmdline
            $object.process.guid = object.process.guid
            $object.process.meta = object.process.meta
            $object.process.original_name = object.process.original_name
            $object.process.cwd = object.process.cwd
            $object.process.chain = object.process.chain

            $object.process.parent.id = object.process.parent.id
            $object.process.parent.name = object.process.parent.name
            $object.process.parent.path = object.process.parent.path
            $object.process.parent.fullpath = object.process.parent.fullpath
            $object.process.parent.guid = object.process.parent.guid
            $object.process.parent.cmdline = object.process.parent.cmdline
        
            $datafield6 = datafield6    # Идентификатор сессии в формате UUID

            $event_src.ip = event_src.ip
            $event_src.hostname = event_src.hostname
            $event_src.fqdn = event_src.fqdn
            $event_src.host = event_src.host
            $event_src.asset = event_src.asset
            $event_src.vendor = event_src.vendor
            $event_src.title = event_src.title
            $event_src.subsys = event_src.subsys
            $event_src.rule = event_src.rule

            $alert.regex_match = "/c echo ..." + regex(lower(object.process.cmdline), "(>>%{1,2}temp%{1,2}\S+\.b64)", 1)

            $first_download_event = false
        endif
    }

emit {
    $correlation_type = "incident"

    $subject = "account"
    $action = "start"
    $object = "process"
    $status = "success"

    $detect = "CVE-2022-41040|CVE-2022-41082"

    $importance = "high"

    $category.generic = "Attack"
    $category.high = "Initial Access"
    $category.low = "Exploit Public-Facing Application"

    $incident.aggregation.key = join([$correlation_name, lower($event_src.host), lower($subject.account.name)], "|")
    $incident.severity = $importance
    $incident.category = "Undefined"
    $incident.aggregation.timeout = 2h
}
